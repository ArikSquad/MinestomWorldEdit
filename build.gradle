buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'JitPack'
            url = 'https://jitpack.io'
        }
    }
    dependencies {
        classpath 'com.fastasyncworldedit:FastAsyncWorldEdit-Libs-Ap:2.11.0'
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

def targetJavaVersion = 21
java {
    toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
}

group = 'eu.mikart.worldedit'
version = '1.0.1-SNAPSHOT'

repositories {
    mavenCentral()
    maven { url = 'https://repo.spongepowered.org/maven' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://maven.enginehub.org/repo/' }
    maven { url = 'https://repo.papermc.io/repository/maven-public/' }
    maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
}

dependencies {
    compileOnly 'net.minestom:minestom-snapshots:f9506f6821'
    compileOnly 'com.google.guava:guava:33.3.1-jre'
    implementation 'com.sk89q.worldedit:worldedit-core:7.3.11-SNAPSHOT'
    implementation 'com.fastasyncworldedit:FastAsyncWorldEdit-Libs-Ap:2.11.0'
    compileOnly 'org.spongepowered:configurate-yaml:4.1.2'
    compileOnly 'org.projectlombok:lombok:1.18.36'

    annotationProcessor 'org.projectlombok:lombok:1.18.36'
}

tasks.named('build').configure {
    dependsOn tasks.named('shadowJar')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = 'eu.mikart.worldedit'
            artifactId = 'worldedit-minestom'
            version = project.version

            pom {
                name = 'WorldEdit Minestom'
                description = 'WorldEdit implementation for Minestom'
                url = 'https://github.com/ArikSquad/worldedit-minestom'

                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'mikart'
            url = uri('https://maven.mikart.eu/snapshots')
            credentials {
                username = System.getenv('MIKART_MAVEN_USER')
                password = System.getenv('MIKART_MAVEN_PASSWORD')
            }
        }
    }
}